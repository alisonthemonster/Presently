apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions' //remove this
apply from: '../versions.gradle'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.facebook.testing.screenshot'
apply plugin: 'dagger.hilt.android.plugin'
apply from: '../gradle/dependency_graph.gradle'

allOpen {
    annotation 'journal.gratitude.com.gratitudejournal.util.OpenClass'
}

android {
    compileSdkVersion project.ext.androidCompileSdk
    defaultConfig {
        applicationId "journal.gratitude.com.gratitudejournal"
        minSdkVersion project.ext.androidMinSdk
        targetSdkVersion project.ext.androidTargetSdk

        versionCode 77
        versionName project.ext.androidVersionName

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        testInstrumentationRunner "journal.gratitude.com.gratitudejournal.testUtils.AppCustomTestRunner"

        Properties localProperties = getLocalProperties()

        String dropboxKey = System.getenv("DROPBOX_APP_KEY") ?: localProperties['DROPBOX_KEY']
        buildConfigField "String", "DROPBOX_APP_KEY", "\"${dropboxKey}\""
        manifestPlaceholders = [dropboxAppKey: dropboxKey]
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    buildTypes {
        debug {
            testCoverageEnabled project.hasProperty('coverage')
            versionNameSuffix "-DEBUG"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }

    kotlinOptions { jvmTarget = "1.8" }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    implementation project(':logging')
    implementation project(':mavericks_utils')
    implementation project(':settings')
    implementation project(':sharing')
    implementation project(':strings')
    implementation project(':presently_local_source')
    implementation project(':date_utils')
    implementation project(':ui')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.4.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.fragment:fragment-ktx:1.4.0-alpha03"
    implementation "androidx.biometric:biometric:1.0.1"
    implementation "androidx.work:work-runtime-ktx:2.4.0"
    implementation 'com.google.android.play:core:1.10.0'
    implementation 'androidx.paging:paging-runtime:3.1.0-alpha01'
    implementation "androidx.room:room-ktx:2.4.0-alpha03"
    implementation "androidx.room:room-runtime:2.4.0-alpha03"
    kapt "androidx.room:room-compiler:2.4.0-alpha03"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation "com.google.android.material:material:1.4.0-rc01"
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.github.sundeepk:compact-calendar-view:3.0.0'
    implementation 'com.dropbox.core:dropbox-core-sdk:4.0.0'
    implementation 'org.apache.commons:commons-text:1.6'
    implementation "org.apache.commons:commons-csv:1.6"

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'

    implementation 'com.airbnb.android:mavericks:2.2.0'
    implementation 'com.airbnb.android:mavericks-mocking:2.2.0'

    //dependency injection
    implementation "com.google.dagger:dagger:2.35"
    kapt "com.google.dagger:dagger-compiler:2.35"
    implementation "com.google.dagger:dagger-android-support:2.35"
    kapt "com.google.dagger:dagger-android-processor:2.35"
    compileOnly "com.squareup.inject:assisted-inject-annotations-dagger2:0.3.2"
    kapt "com.squareup.inject:assisted-inject-processor-dagger2:0.3.2"
    implementation 'com.google.dagger:hilt-android:2.38.1'
    kapt 'com.google.dagger:hilt-android-compiler:2.38.1'

    testImplementation project(':testing')
    testImplementation 'junit:junit:4.13'
    testImplementation "androidx.room:room-testing:2.3.0-beta01"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_version"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    testImplementation 'com.airbnb.android:mavericks-testing:2.0.0'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation ('org.threeten:threetenbp:1.4.1'){
        exclude group:'com.jakewharton.threetenabp', module:'threetenabp'
    }

    androidTestImplementation project(':testing')
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation "androidx.room:room-testing:2.3.0-beta01"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_version"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    androidTestImplementation "androidx.work:work-testing:2.4.0"
    debugImplementation project(':testing') //needed for the hilt test activity


    androidTestImplementation "com.google.dagger:hilt-android-testing:2.38.1"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:2.38.1"

    androidTestUtil 'com.android.support.test:orchestrator:1.0.2'

    debugImplementation "androidx.fragment:fragment-testing:1.3.0-alpha08"

}

apply plugin: 'com.google.gms.google-services'


def getLocalProperties() {
    Properties props = new Properties()
    if (file('../local.properties').exists()) {
        props.load(new FileInputStream(file('../local.properties')))
    }
    return props
}