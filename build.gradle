// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        apply from: 'versions.gradle'
        google()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "com.google.gms:google-services:$google_services_version"
        classpath "io.fabric.tools:gradle:$fabric_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.1.0-alpha02"
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.google.com/'
        }
    }

    project.ext.androidVersionName = createVersionName()
    project.ext.buildNumber = getBuildNumber()
    project.ext.branchName = getBranchName()
}

def createVersionName() {
    def buildNumber = getBuildNumber()
    def patchNumber = 0
    if (getBranchName().startsWith("release/")) {
        patchNumber = buildNumber - 1
    }
    return major + "." + minor + "." + patchNumber
}

String getBranchName() {
    return project.hasProperty("branchName") ? ((String) project.properties['branchName']).toString() : ""
}

//get the build number from the build server
String getBuildNumber() {
    return project.hasProperty("buildNumber") ? ((String) project.properties['buildNumber']).toString() : ""
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// TODO add detekt